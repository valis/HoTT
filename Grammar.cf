entrypoints Defs;

comment "--";
comment "{-" "-}";

layout "let";
layout stop "in";
layout toplevel;

Defs.      Defs ::= [Def];
Def.       Def ::= PIdent [Arg] "=" Expr;
DefType.   Def ::= PIdent ":" Expr;

separator  Def ";";

Let.      Expr  ::= "let" "{" [Def] "}" "in" Expr;
Lam.      Expr  ::= PLam [Binder] "->" Expr;
Arr.      Expr1 ::= Expr2 "->" Expr1;
Pi.       Expr1 ::= [TypedVar] "->" Expr1;
Typed.    Expr1 ::= Expr4 "::" Expr1;
Prod.     Expr2 ::= Expr3 "*" Expr2;
Sigma.    Expr2 ::= [TypedVar] "*" Expr2;
Id.       Expr3 ::= Expr4 "=" Expr4;
Pair.     Expr4 ::= Expr4 "," Expr5;
App.      Expr5 ::= Expr5 Expr6;
Var.      Expr6 ::= Arg;
Nat.      Expr6 ::= PNat;
Suc.      Expr6 ::= PSuc;
Rec.      Expr6 ::= PR;
Idp.      Expr6 ::= PIdp;
Ext.      Expr6 ::= PExt;
Pmap.     Expr6 ::= Ppmap;
Coe.      Expr6 ::= PCoe;
Proj1.    Expr6 ::= PProjl;
Proj2.    Expr6 ::= PProjr;
Iso.      Expr6 ::= PIso;
Comp.     Expr6 ::= PComp;
Inv.      Expr6 ::= PInv;
InvIdp.   Expr6 ::= PInvIdp;
NatConst. Expr6 ::= PInt;
Universe. Expr6 ::= U;
_.        Expr  ::= Expr1;
_.        Expr1 ::= Expr2;
_.        Expr2 ::= Expr3;
_.        Expr3 ::= Expr4;
_.        Expr4 ::= Expr5;
_.        Expr5 ::= Expr6;
Paren.    Expr6 ::= PPar Expr ")";

Arg.       Arg ::= PIdent;
NoArg.     Arg ::= Pus;
terminator Arg "";

-- Like Arg, but cannot be empty
Binder.   Binder ::= Arg;
separator nonempty Binder "";

TypedVar. TypedVar ::= PPar Expr ":" Expr ")";
terminator nonempty TypedVar "";

position token U ('T' 'y' 'p' 'e' digit*);
position token PLam '\\';
position token PPar '(';
position token PInt digit+;
position token PIdp 'i' 'd' 'p';
position token PR 'R';
position token PSuc 's' 'u' 'c';
position token PNat 'N' 'a' 't';
position token Pus '_';
position token PExt 'e' 'x' 't';
position token Ppmap 'p' 'm' 'a' 'p';
position token PCoe 'c' 'o' 'e';
position token PProjl 'p' 'r' 'o' 'j' '1';
position token PProjr 'p' 'r' 'o' 'j' '2';
position token PIso 'i' 's' 'o';
position token PComp 'c' 'o' 'm' 'p';
position token PInv 'i' 'n' 'v';
position token PInvIdp 'i' 'n' 'v' 'I' 'd' 'p';
position token PIdent (letter(letter|digit|'\''|'_')*);
